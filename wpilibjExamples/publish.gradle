apply plugin: 'maven-publish'

def pubVersion
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

def baseExamplesArtifactId = 'examples'
def baseTemplatesArtifactId = 'templates'
def baseCommandsArtifactId = 'commands'
def artifactGroupId = 'edu.wpi.first.wpilibj'

def outputsFolder = file("$project.buildDir/outputs")

def javaExamplesZipTask = tasks.register('javaExamplesZip', Zip) {
    destinationDir = outputsFolder
    baseName = 'wpilibj-examples'

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/examples') {
        into 'examples'
    }
}

def javaTemplatesZipTask = tasks.register('javaTemplatesZip', Zip) {
    destinationDir = outputsFolder
    baseName = 'wpilibj-templates'

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/templates') {
        into 'templates'
    }
}

def javaCommandsZipTask = tasks.register('javaCommandsZip', Zip) {
    destinationDir = outputsFolder
    baseName = 'wpilibj-commands'

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/commands') {
        into 'commands'
    }
}

tasks.named('build').configure {
    dependsOn javaTemplatesZipTask
    dependsOn javaExamplesZipTask
    dependsOn javaCommandsZipTask
}


publishing {
    publications {
        examples(MavenPublication) {
            artifact javaExamplesZip

            artifactId = baseExamplesArtifactId
            groupId artifactGroupId
            version pubVersion
        }

        templates(MavenPublication) {
            artifact javaTemplatesZip

            artifactId = baseTemplatesArtifactId
            groupId artifactGroupId
            version pubVersion
        }

        commands(MavenPublication) {
            artifact javaCommandsZip

            artifactId = baseCommandsArtifactId
            groupId artifactGroupId
            version pubVersion
        }
    }
}
